title: InspectingVoid
tags: 
colorID: 5
position: -3318,-3519
---
Player: The void
Player: Everyday, it inches closer
Player: Why would the Phoenix rush beyond the barrier?
Player: I shouldn't meddle in the decisions of the Divine.
Player: It's safe here in town.
Player: It's safe here for now.
<<Transition WakeUp>>
===
title: DFL
tags: 
colorID: 8
position: -3285,-2809
---
<<if $lured_out is false>>
    [[DFLVillage]]
<<elseif $lured_out is true>>
    [[DFLEnd]]
<<endif>>
===
title: DFLVillage
tags: 
colorID: 2
position: -3448,-2343
---
<<if visited("DFLVillage")>>
    [[DFLVillage_Personal]]
<<else>>
    DFL: Our Heavenly Phoenix is gone. 
    DFL: She vanished last night.
    DFL: It's too late.
    DFL: Whatever is out there. It's going to swallow us.
    DFL: Whole.
    Widow: I lost my husband.
    Widow: My husband, my daughter, and my mother
    Widow: To whatever this...thing is.
    Clubman: We've all lost people.
    Clubman: Your grief is no more than any other.
    DFL: We aren't discussing who lost the most.
    DFL: The question is...
    DFL:Should we not end it now?
    Widow: What do you mean?
    Widow: As in?
    DFL: Self-slaughter, yes.
    DFL: Deny whatever this is the pleasure of consumption.
    Widow: Perhaps...I'm all alone. Perhaps...
    Clubman: Come! Baker! 
    Clubman: What do you make of this madness!
    -> I agree with his sentiment
        <<set $agree_with_leader to true>>
        Player: I think it's for the best.
        Player: We die by the barrier, or we die by our own hands.
        Widow: So there's no hope? No hope to change anything?
        Player: I don't think so. No.
        DFL: That's what I thought as well.
        DFL: Let us take this last night.
        DFL: Tomorrow, those who wish to leave this world...
        DFL: Can do so.
        Clubman: I can't do it. I won't do it. There must be something.
        Clubman: Somewhing we can take back.
    -> No
        <<set $agree_with_leader to false>>
        Player: I saw one of the creatures.
        Widow: The pray men?
        Player: Yes. It called me Malignant.
        DFL: Malignant?
        Player: It viewed me as a tumor.
        Player: It views us all as tumors.
        DFL: And what does this mean?
        Player: It means we can't let something that views us as
        Player: no more than tumors...destroy us.
        Widow: Thank God.
        Widow: Someone with reason.
        DFL: It is foolish to believe that an ant
        DFL: given enough hope
        DFL: will ever triumph over the foot of an elephant
        <<wait 1>>
        DFL: But fine, those who wish to perish by the barrier
        DFL: do so.
        DFL: Those who wish to choose their passing,
        DFL: take this last night
        DFL: and we will congregate here tomorrow.
    <<set $leader_encourage_sleep to true>>
<<endif>>
===
title: DFLVillage_Personal
tags: 
colorID: 3
position: -3436,-2078
---
<<if visited("DFLVillage_Personal")>>
    DFL: There's nothing left to say.
    DFL: Nothing matters anymore after tomorrow's dawn.
<<else>>
    DFL: Baker
    <<if $agree_with_leader is true>>
        DFL: I'm glad we agree earlier Baker
        DFL: I hope you will join your Poiura on the other side
    <<else>>
        DFL: I know you disapprove my methods
        DFL: But try managing an entire village in crisis
        DFL: It's just ...
        DFL: So tiring
    <<endif>>
<<endif>>
===
title: DFLEnd
tags: 
colorID: 2
position: -2981,-2808
---
<<if visited("DFLEnd")>>
    [[AfterSpawn]]
<<else>>
    DFL: You!
    DFL: You brought this on us.
    Player: How?
    DFL: With your defiance of order!
    DFL: I needed your help in town.
    DFL: You disobeyed!
    Player: And yet you are here.
    DFL: I was lured.
    DFL: Like you.
    DFL: Brainwashed.
    DFL: You're insolence made me weak.
    DFL: You ruined our self-slaughter!
    <<Spawn SecondGaki>>
<<endif>>
===
title: DayBed
tags: 
colorID: 5
position: -3769,-3510
---
<<if $leader_encourage_sleep>>
    Player: I guess this is it.
    <<Transition WakeUp>>
<<else>>
    Player: I'm not sleepy yet.
<<endif>>
===
title: Beckoning
tags: 
colorID: 7
position: -4427,-3066
---
Player: Poiura
Player: I miss you
<<PlayTimeline Beckoning>>
<<wait 2>>
Hand: Come
-> Why are you doing this?
    <<PlayTimeline Beckoning>>
    Hand: Presumptuous malignant
-> Are you doing this?
    <<PlayTimeline Beckoning>>
    Hand: It's happening
Player: The others intend to die before you can kill them
Hand: Cancer kills themselves
Hand: Now come
-> What do I stand to benefit?
    Hand: Come
-> I'm not coming with you
    Hand: Ignorant malignant
<<PlayTimeline DFLAppear>>
<<wait 1>>
DFL: Wait
DFL: What are you doing?
DFL: Where are you going?
-> I'm not sure.
    Player: I don't know.
    DFL: Don't give in!
    DFL: Perhaps this is how!
    DFL: How it's killing us!
    Player: What if it isn't?
    DFL: What? Of course it is!
    Player: I spoke to it, again.
    DFL: It's tricking you.
    Player: I think it's helping me.
    DFL: You fool!
    Player: You're the fool.
    Player: You hear that? It's comforting us.
    DFL: You cannot take comfort in that awful cry.
    Player: Ignorant Malignant.
-> Beyond the Barrier.
    Player: It called out to me.
    DFL: Don't give in!
    DFL: Perhaps this is how!
    DFL: How it's killing us!
    Player: Cancer cells kill themselves.
<<set $lured_out to true>>
<<Transition Sautdanslevide>>
===
title: EquipGun
tags: 
colorID: 5
position: -4199,-3508
---
<<Equip Player Blunderbuss>>
<<set $got_gun to true>>
<<PlayOneShot GB_AudioManager Pickup>>
Player: This will come in handy
===
title: Phoenix
tags: 
colorID: 8
position: -4135,-2835
---
<<if $collect_trinket_1 is true>>
    [[PhoenixLastStand]]
<<elseif $trinket_3 is false>>
    [[PhoenixBefore]]
<<endif>>
===
title: PhoenixBefore
tags: 
colorID: 0
position: -3825,-2831
---
<<if visited("PhoenixBefore")>>
    Phoenix: ...
<<else>>
    Phoenix: They worship youth. Fear death.
    Phoenix: I'm not Heavenly.
    Phoenix: Oh! You. Baker.
    -> Our Phoenix! You're alive?
        Player: This place...I cannot believe you are still alive.
        Phoenix: Alive? No, I'm not. It's gone. All gone.
        Phoenix: The raven has flown, cawing across the land.
        Phoenix: Terrorist Raven caws down to me. 
    -> What are you doing here?
        Phoenix: What is it doing here? What's the difference?
        Phoenix: Life stuck behind the barrier is the same as beyond.
        Phoenix: The raven has flown, cawing across the land.
        Phoenix: A terrorist raven. Politically motivated.
        Phoenix: The town is dead.
    Player: What?
    Phoenix: The void is as warm as it is cold.
    Phoenix: Don't you think?
    Player: You mean where we are?
    Phoenix: Leave me alone.
    <<set $talk_with_princess to true>>
<<endif>>
===
title: Effigy
tags: 
colorID: 8
position: -4763,-2805
---
<<if $collect_trinket_2>>
    Player: This is an error. 
<<elseif $collect_trinket_1>>
    [[Effigy2]]
<<else>>
    [[Effigy1]]
<<endif>>
===
title: Effigy1
tags: 
colorID: 5
position: -5072,-2805
---
-> Leave it 
-> Touch it
Effigy: Poiura
Player: What? 
Effigy: Poiura
Player: How do you know that name?
Effigy: I am Poiura
Player: Tell me how you know this name. Where is Poiura?
<<set $collect_trinket_1 to true>>
===
title: Effigy2
tags: 
colorID: 5
position: -4758,-2547
---
Player: Are you what I was told to collect?
Effigy: Poiura
Player: Foul trinket!
<<set $collect_trinket_2 to true>>
===
title: Portal
tags: 
colorID: 6
position: -4880,-3431
---
Player: Phoenix, I believe I've found the last of your trinkets.
Phoenix: Spoiled wastes. Cleanse the aftermath.
Phoenix: That's what they told me.
Player: What?
Phoenix: You think I wanted this?
Phoenix: Deification.
Phoenix: Beyond the world, there is nothing.
-> Faith has no reward.
    Player: Faith takes value beyond the reward of afterlife.
    Phoenix: Then what good is it?
    Phoenix: The void takes you either way.
    Phoenix: Off to the land of little birdies cawing.
    //screenshake on caw caw caw?
    Phoenix: Caw, caw, caw.
-> Please try.
Player: Please, we must try.
Phoenix: And what of your Poiura?
Player: Poiura is gone. I'm tired of being taunted.
Phoenix: But don't you wish for Poiura to return?
-> No
    Player: If Poiura is out here...No.
    Player: What I want is for life to continue.
    Player: There's no point to changing the past. Just the present.
    Player: I just want to bake another loaf of bread.
    Player: So please, try.
Phoenix: I am finished. But if you care about one more moment.
Phoenix: Give them here.
<<PlayTimeline EndingScene>>
Phoenix: Our elders.
Phoenix: You are liars.
Phoenix: Nasty, awful liars.
Phoenix: Make up for your past misgiving's.
Phoenix: Use these little trinkets to excise us from this hell.
Player: It's working.
Phoenix: It's not. This is the void tricking us.
Phoenix: It is you who led your people astray.
Phoenix: False promises.
Phoenix: I failed my test of faith.
Phoenix: But I can still save this baker.
<<set $portal_end_portal to true>>
Phoenix: Oh my god! It worked! The elders!
<<set $portal_end_hand to true>>
<<wait 5>>
Hand: Malignant! 
Hand: Join me
-> Accept
    Player: I don't fear you
    Hand: You ought to
    Player: But I donâ€™t. You are a manifestation of the void. 
    Player: You are an earthquake, a black hole, a super nova
    Hand: Come
    <<Equip Player NONE>>
    Hand: No weapon?
    Player: No need
    Hand: Then how will you defeat me?
    Player: I can either end my own life or have a cup of coffee.
    -> End life
        Hand: Then join your precious Poiura
    -> Have cup of coffee
        Hand: Poiura
        Player: I choose the cup of coffee.
-> Refuse
    Hand: No, you say.
    Hand: Then join your precious Poiura
===
title: PhoenixLastStand
tags: 
colorID: 0
position: -3821,-2608
---
<<if visited("PhoenixLastStand")>>
    Phoenix: ...
<<else>>
    Player: What do we do?
    Phoenix: Heh, join the void.
    Player: You must know something more.
    Player: Where are we?
    Phoenix: This place is written.
    Phoenix: It is beyond death.
    Phoenix: It is reserved for the willing dead.
    Player: How do we get out of here?
    Phoenix: I've only heard of such things from the village elders.
    Phoenix: There are gifts, embedded in shadows. Small trinkets spread
    Phoenix: throughout the void.
    Phoenix: There's a ritual. You can sacrifice
    Phoenix: the gifts. If you do, the spirits may.
    -> I don't understand.
        Player: Please speak some sense.
        Phoenix: You wouldn't. Staking your salvation in youth.
        Player: I do not.
        Phoenix: Your elders do. They may grant a wish.
    -> May what?
        Player: What will they do?
        Phoenix: Grant a wish. Though...
    Player: I will find the trinkets.
    Phoenix: You will not.
    Player: Where will I find you?
    Phoenix: Oh, around. I will be around.
    <<set $talk_with_princess_2 to true>>
<<endif>>
===
title: PhoenixChase
tags: 
colorID: 6
position: -4903,-3156
---
<<PlayTimeline PhoenixChase>>
<<set $princess_seen to true>>
<<wait 6>>
Player: It's the young Phoenix
Player: Where could she be going in the middle of the night?
Player: And why towards that dreadful barrier
===
title: Clubman
tags: 
colorID: 8
position: -3800,-3150
---
<<if visited("Clubman")>>
    Clubman: I'll bash them in good
<<else>>
    Clubman: Anything come beyond that barrier to take what's mine?
    Clubman: I'll club it dead
    -> Fair enough
        Player: Let's see them try
    -> We're doomed
        Player: I don't think a club will stop the barrier
    Clubman: What do you know?
<<endif>>
===
title: Widow
tags: 
colorID: 8
position: -3483,-3158
---
<<if visited("Widow")>>
    [[DeepWidow]]
<<else>>
    Widow: Hello
    Player: Good evening.
    Widow: It isn't good, is it?
    Player: No, I suppose not.
    Widow: What do you think happens to them?
    Player: Who?
    Widow: Those consumed by the barrier.
    -> I think they're dead.
        Player: They may as well be.
        Widow: What a horrible thing to say.
    -> I think we can save them.
        Player: Who knows? Maybe this is our reckoning.
        Player: Maybe they're raptured. Maybe they're warm and safe.
    Widow: I hope that is true.
    Widow: For all of us.
    Player: Me too.
<<endif>>
===
title: DeepWidow
tags: 
colorID: 1
position: -3185,-3165
---
<<if visited("DeepWidow")>>
    Widow: I pray that you won't be the next to be taken.
<<else>>
    Widow: I should go to bed.
    Player: I should, too.
    Widow: Do you miss Poiura?
    -> Of course.
    Player: Not a day goes by.
    -> Every day.
    Player: The bakery just isn't the same.
    Widow: My husband...a Hateful man. 
    Widow: But I miss him.
    Widow: Was Poiura hateful?
    Player: No. Not at all.
<<endif>>
===
title: AfterSpawn
tags: 
colorID: 3
position: -2673,-2813
---
<<if visited("AfterSpawn")>>
    DFL: Traitor ...
    Player: He's in a daze.
    Player: There's no helping him now.
<<else>>
    DFL: I trust nothing from you.
    DFL: Was it not Poiura who first vanished?
    DFL: Perhaps you two are the cause for all of it!
    Player: Watch yourself. Poiura had nothing to do with this.
    DFL: Says you. The traitor.
<<endif>>
===
title: GakiFirstEncounter
tags: 
colorID: 6
position: -4749,-2212
---
Gaki: That foul, false savior sends a humble baker to collect effigies on her behalf? 
Gaki: How lazy and pedantic
Player: I will defeat you
Gaki: She knows where they all are, you know
Gaki: These little trinkets are nothing to me
Gaki: False hope for a false people
Gaki: Cancer cells kill themselves
===
